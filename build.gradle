apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}

dependencies {
    testCompile 'org.assertj:assertj-core:3.9.0'

    testCompile 'io.cucumber:cucumber-java8:4.3.0'
    testCompile 'io.cucumber:cucumber-junit:4.3.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

// Code coverage
jacoco {
    toolVersion = '0.8.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        xml.destination "${buildDir}/reports/jacoco/report.xml"
    }
}

repositories {
    mavenCentral()
}

check.dependsOn jacocoTestReport